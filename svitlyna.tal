|00 @System [ &vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2 &b $2 &debug $1 &state $1 ]
|10 @Console [ &vector $2 &read $1 &pad $4 &type $1 &write $1 &error $1 ]
|20 @Screen [ &vector $2 &width $2 &height $2 &auto $1 &pad $1 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1 ]

|70 
@width $2 @height $2

|a0 @File [ &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2 ]


( %DEBUG { DUP .Console/write DEO } )
( %DEBUG-BYTE { DUP print-byte } )

%DEBUG { #00 POP }
%DEBUG-BYTE { #00 POP }

|0100
	( hardcoded colors for now )
	#da73 .System/r DEO2
	#d753 .System/g DEO2
	#d644 .System/b DEO2

	;filename .File/name DEO2
	#000e .File/length DEO2 ;buffer .File/read DEO2
	.File/success DEI2 #000e EQU2 ?{ !bad-file }

	;buffer/magic LDA DEBUG DUP LIT "f EQU ?{ !bad-file }

	#0000
@ff-header
	INC2 DUP2 DUP2
	;&magic ADD2 LDA DUP ?{ !ff-size }
	ROT ROT ;buffer ADD2 LDA DEBUG EQU ?{ !bad-file }
	!ff-header
	&magic "farbfeld 00
@ff-size
	;buffer/ffwidthhigh LDA2 #0000 EQU2 ?{ !too-large }
	;buffer/ffwidthlow LDA2 DUP2 .width STZ2 .Screen/width DEO2
	;buffer/ffheighthigh LDA2 #0000 EQU2 ?{ !too-large }
	#0002 .File/length DEO2 ;buffer/ffheightlow .File/read DEO2
	;buffer/ffheightlow LDA2 DUP2 .height STZ2 .Screen/height DEO2
	
	#01 .Screen/auto DEO
	#0008 .File/length DEO2

	;ff-file-print-bunch .Screen/vector DEO2
	BRK


@ff-file-print-bunch
	&loop
		ff-file-print
		!&loop
	!fail

	
( 16-bit depth, not alpha-premultiplied; we discard the lower 12 bits )
( 4 bit color multiplied with 4 bit alpha to get 8 bits )
@ff-buffer [ &r $2 &g $2 &b $2 &a $2 ]
@ff-file-print
	;ff-buffer .File/read DEO2
	,ff-buffer/a LDR INC ?{
		,ff-buffer/r LDR #04 SFT ,ff-buffer/a LDR #04 SFT MUL ;color/r STA
		,ff-buffer/g LDR #04 SFT ,ff-buffer/a LDR #04 SFT MUL ;color/g STA
		,ff-buffer/b LDR #04 SFT ,ff-buffer/a LDR #04 SFT MUL ;color/b STA
		print-pixel
		JMP2r
	}
	,ff-buffer/r LDR ;color/r STA
	,ff-buffer/g LDR ;color/g STA
	,ff-buffer/b LDR ;color/b STA
	print-pixel
	JMP2r


( assumes screen auto x, RGB written to ;color )
@print-pixel
	pick-color 
	( LIT "! .Console/write DEO DUP #30 ADD .Console/write DEO )
	.Screen/pixel DEO
	.Screen/x DEI2 
	( LIT "x .Console/write DEO DUP2 print-short #0a .Console/write DEO )
	.width LDZ2
	NEQ2 ?{ #0000 .Screen/x DEO2 .Screen/y DEI2 INC2 DUP2 .height LDZ2 NEQ2 ?{ !end } .Screen/y DEO2 }

	JMP2r
	

( chooses closest color from palette )
@pick-color
	#00 [ ;color/r LDA ] [ ,color0/r LDR ] GTHk ?{ SWP } SUB
	#00 [ ,color/g LDR ] [ ,color0/g LDR ] GTHk ?{ SWP } SUB ADD2
	#00 [ ,color/b LDR ] [ ,color0/b LDR ] GTHk ?{ SWP } SUB ADD2
	,color/mindist STR2 #00 ;color/mindistcol STA
	
	#0000
	#00 [ ,color/r LDR ] [ ,color1/r LDR ] GTHk ?{ SWP } SUB
	#00 [ ,color/g LDR ] [ ,color1/g LDR ] GTHk ?{ SWP } SUB ADD2
	#00 [ ,color/b LDR ] [ ,color1/b LDR ] GTHk ?{ SWP } SUB ADD2
	DUP2 ,color/mindist LDR2 GTH2 ?{ DUP2 ,color/mindist STR2 #01 ,color/mindistcol STR } POP2
	
	!pick-color2
( hardcoded )
@color0 [ &r d0 &g d0 &b d0 ]
@color1 [ &r 90 &g 70 &b 60 ]
@color2 [ &r 70 &g 50 &b 40 ]
@color3 [ &r 30 &g 30 &b 30 ]
@color [ &r 00 &g 00 &b 00 &mindist ffff &mindistcol 00 ]
@pick-color2
	#0000
	#00 [ ,color/r LDR ] [ ,color2/r LDR ] GTHk ?{ SWP } SUB
	#00 [ ,color/g LDR ] [ ,color2/g LDR ] GTHk ?{ SWP } SUB ADD2
	#00 [ ,color/b LDR ] [ ,color2/b LDR ] GTHk ?{ SWP } SUB ADD2
	DUP2 ,color/mindist LDR2 GTH2 ?{ DUP2 ,color/mindist STR2 #02 ,color/mindistcol STR } POP2
	
	#0000
	#00 [ ,color/r LDR ] [ ,color3/r LDR ] GTHk ?{ SWP } SUB
	#00 [ ,color/g LDR ] [ ,color3/g LDR ] GTHk ?{ SWP } SUB ADD2
	#00 [ ,color/b LDR ] [ ,color3/b LDR ] GTHk ?{ SWP } SUB ADD2
	DUP2 ,color/mindist LDR2 GTH2 ?{ DUP2 ,color/mindist STR2 #03 ;color/mindistcol STA } POP2

	( ;color/mindist LDA2 print-short )
	( ;color/mindistcol LDA #30 ADD .Console/write DEO )
	;color/mindistcol LDA
	JMP2r
	

( https://wiki.xxiivv.com/site/uxntal_library.html )
@print-short ( short* -- )
	SWP print-short/b
	&b ( -- )
		DUP #04 SFT print-short/c
	&c ( -- )
		#0f AND DUP #09 GTH #27 MUL ADD [ LIT "0 ] ADD .Console/write DEO
		JMP2r

@print-str ( str* -- )
	&while ( -- )
		LDAk .Console/write DEO
		INC2 LDAk ?&while
	POP2 JMP2r
	

@bad-file 
	;&str !fail
	&str "...Bad 20 "file 00

@too-large
	;&str !fail
	&str "...Too 20 "large 00

@fail ( error-msg* -- )
	print-str
	#01 .System/state DEO
	BRK
	
@end
	;&str print-str #0000 .Screen/vector DEO2 BRK
	&str "Готово! 00

@start-str "Hi 20 "there! 00

@print-byte ( b -- )
	DUP #04 SFT DUP #09 GTH #27 MUL ADD [ LIT "0 ] ADD .Console/write DEO
	#0f AND DUP #09 GTH #27 MUL ADD [ LIT "0 ] ADD .Console/write DEO
	JMP2r


@filename "image.ff 00
@buffer [
	&magic                       $4
	              &qoiwidthhigh  $2
	              &qoiwidthlow   $2
	&ffwidthhigh  &qoiheighthigh $2
	&ffwidthlow   &qoiheightlow  $2
	&ffheighthigh &qoichan       $1
	              &qoispace      $1
	&ffheightlow                 $2
]
@imagelines
